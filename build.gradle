import org.ajoberstar.grgit.Grgit

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        // Add dependency for build script,
        // so we can access Git from our
        // build script.
        classpath 'org.ajoberstar:grgit:1.1.0'
    }
}

apply plugin: 'java'
apply plugin: 'maven'

group = 'xyz.rc24.bot'
version = '2.0.0'

description = """"""

sourceCompatibility = 1.8
targetCompatibility = 1.8
tasks.withType(JavaCompile) {
	options.encoding = 'UTF-8'
}

repositories {
     jcenter()
}
dependencies {
    compile group: 'net.dv8tion', name: 'JDA', version: '3.3.1_286'
    compile group: 'com.jagrosh', name: 'JDA-Utilities', version: '1.8'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.9.0'
    compile group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-yaml', version: '2.9.0'
    compile group: 'redis.clients', name: 'jedis', version: '2.9.0'
    compile group: 'com.google.code.gson', name: 'gson', version: '2.8.1'
    compile group: 'org.jsoup', name: 'jsoup', version: '1.10.3'
    compile group: 'ch.qos.logback', name: 'logback-classic', version: '0.9.26'
    compile group: 'org.codehaus.groovy', name: 'groovy-jsr223', version: '2.4.5'
    compile group: 'commons-io', name: 'commons-io', version: '2.4'

    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.powermock', name: 'powermock-module-junit4', version: '1.6.6'
    testCompile group: 'org.powermock', name: 'powermock-api-mockito2', version: '1.6.6'
}

// Fix jar + main class
jar {
    manifest {
        attributes(
                'Class-Path': configurations.compile.collect { it.getName() }.join(' '),
                'Main-Class': 'xyz.rc24.bot.RiiConnect24Bot',
                'Implementation-Title': 'RC24 Bot',
                'Implementation-Version': version,
        )
    }
    from { (configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }) } {
        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
    }
    from "$buildDir/resources/info.properties"
}

task createBuildInfoFile(dependsOn: processResources) {
    doLast {
        def buildInfoFile = new File("$buildDir/resources/info.properties")
        Properties props = new Properties()
        props.setProperty('commit', Grgit.open(file('.')).head().abbreviatedId)
        props.store(buildInfoFile.newWriter(), null)
    }
}

classes {
    dependsOn createBuildInfoFile
}